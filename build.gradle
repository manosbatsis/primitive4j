import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'java'
	alias(libs.plugins.spring.boot) apply(false)
	alias(libs.plugins.spotless) apply(false)
	alias(libs.plugins.freefair.lombok) apply(false)
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
	sourceCompatibility = JavaVersion.VERSION_23
	targetCompatibility = JavaVersion.VERSION_1_8
}


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

subprojects() {

	apply plugin: 'java-library'
	apply plugin: 'io.freefair.lombok'
	apply plugin: 'com.diffplug.spotless'

	spotless {
		java {
			removeUnusedImports()
			cleanthat().sourceCompatibility(java.sourceCompatibility.toString())
			googleJavaFormat('1.25.2')
					.aosp()
					.reflowLongStrings()
					.formatJavadoc(false)
					.reorderImports(true)
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		// Delegate versions to Spring BOM
		implementation platform(SpringBootPlugin.BOM_COORDINATES)

		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
		testImplementation("org.junit.jupiter:junit-jupiter")
		testImplementation("org.assertj:assertj-core")

	}

	test {
		useJUnitPlatform()
		testLogging {
			showStandardStreams = true
			events "passed", "skipped", "failed"
		}
	}
}